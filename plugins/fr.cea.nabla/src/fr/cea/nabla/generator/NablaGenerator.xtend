/*******************************************************************************
 * Copyright (c) 2020 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/
package fr.cea.nabla.generator

import com.google.inject.Inject
import fr.cea.nabla.generator.NablaGeneratorMessageDispatcher.MessageType
import fr.cea.nabla.nabla.NablaModule
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext

import static extension fr.cea.nabla.LatexLabelServices.*
import fr.cea.nabla.ir.Utils

class NablaGenerator extends AbstractGenerator
{
	@Inject NablaGeneratorMessageDispatcher dispatcher

	override doGenerate(Resource input, IFileSystemAccess2 fsa, IGeneratorContext context)
	{
		try
		{
			val module = input.contents.filter(NablaModule).head
			if (!module.jobs.empty)
			{
				val latexFileName = module.name.toLowerCase + '/' + module.name + '.tex'
				fsa.generateFile(latexFileName, module.latexContent)
				dispatcher.post(MessageType.Start, 'LaTeX formula written in: ' + latexFileName)
			}
		}
		catch(Exception e)
		{
			dispatcher.post(MessageType.Error, e.message)
			dispatcher.post(MessageType.Error, Utils::getStackTrace(e))
			throw(e)
		}
	}

	private def getLatexContent(NablaModule m)
	'''
		\documentclass[11pt]{article}

		\use package{fontspec}
		\use package{geometry}
		\geometry{landscape}

		\title{Nabla Module «m.name»}
		\author{Generated by the NabLab environment}

		\begin{document}
		\maketitle

		«FOR j : m.jobs»
		«val latexContent = j.latex»
		«IF !latexContent.nullOrEmpty»

		\section{«j.name»}
		$«latexContent»$

		«ENDIF»
		«ENDFOR»
		\end{document}
	'''
}
